// compile me in the cmd prompt:
// pegjs -O speed lib/ingredientLineParser.pegjs

// for codepen:
// pegjs --format globals -e Parser -O size ingredientLineParser.pegjs

// for running in cli
// node --require ./lib/ingredientLineParser
//> var Parser = require('./lib/ingredientLineParser');

// TODO generate spaces/dashes as: $("hot"i _* dashes* "sauce"i)
/*
  list = list.map(str => {
  	str = str.toLowerCase();
		const split = str.split(/[ -]+/);
		if (split.length === 2) {
			return [
				`${split[0]} ${split[1]}`,
				`${split[0]}-${split[1]}`,
				`${split[0]}${split[1]}`,
				];
	    }
		return [ str ];
	});
	list = [ ...new Set(list.flat()) ];
	list.sort(function(a, b) { return b.length - a.length || b.localeCompare(a) });
	copy(list);

*/

{
	var formatFillerExpression = function(symbols0, ws, words, symbols1) {
		var filler = "";
		if (symbols0 && symbols0.length > 0) {
			filler += symbols0;
		}
		if (ws && ws.length > 0) {
			filler += ' ';
		}
		if (words && words.length > 0) {
			filler += words;
		}
		if (symbols1 && symbols1.length > 0) {
			filler += symbols1;
		}

		return filler.toLowerCase().split(',').join('');
	}
}

start =
  ingredientLines

/*==================================
=            Base Rules            =
==================================*/
_ "Whitespace" =
	[ \t\n\r\x20] { return ' '; }

dashes "Dashes" =
	[-_~] { return '-'; }
	/ [‐‑‒–—] { return '-'; } // unicode options

slashes "Slashes" =
	[/\⁄|] { return '\/'; }

quotes "Quotes" =
	["'`´‘’“”] { return '\''; }

letter "Letter" =
 [a-z]i
 / quotes
 / [©™]

// TODO think this through more
//letterExclusion "Letter Exclusion" =
//	!digit !dashes !_ !slashes ![,.;:] !fillerPunctuation !containerIndicator !symbolSeparator char:.
//		{ return char; }

digit "Digit" =
  [0-9]
  // unicode sub and superscripts
  / '⁰' { return 0; }
  / '¹' { return 1; }
  / '⁴' { return 4; }
  / '⁵' { return 5; }
  / '⁶' { return 6; }
  / '⁷' { return 7; }
  / '⁸' { return 8; }
  / '⁹' { return 0; }
  / '₀' { return 0; }
  / '₁' { return 1; }
  / '₂' { return 2; }
  / '₃' { return 3; }
  / '₄' { return 4; }
  / '₅' { return 5; }
  / '₆' { return 6; }
  / '₇' { return 7; }
  / '₈' { return 8; }
  / '₉' { return 9; }

unicodeAmount "Unicode Amount" =
	'½' { return '1/2'; } / '⅓' { return '1/3'; } / '⅔' { return '2/3'; } / '¼' { return '1/4'; } / '¾' { return '3/4'; } / '⅕' { return '1/5'; } / '⅖' { return '2/5'; } / '⅗' { return '3/5'; } / '⅘' { return '4/5'; } / '⅙' { return '1/6'; } / '⅚' { return '5/6'; } / '⅛' { return '1/8'; } / '⅜' { return '3/8'; } / '⅝' { return '5/8'; } / '⅞' { return '7/8'; }

fraction "Fraction" =
	numerator:digit+ _* slashes+ _* denominator:digit+
		{ return numerator + '/' + denominator; }

float "Float" =
	// i think its a british thing to use commas; i see a lot of 1,5 kg
	head:$(digit)* [.,] tail:$(digit)+
		{ return head + '.' + tail; }


/*----------  Indicators & Separators  ----------*/
separator "Separator" =
	symbolSeparator
	/ sep:wordSeparator
		{ return sep.toLowerCase(); }

amountSeparator "Amount Separator" =
	sep:wordSeparator
		{ return sep.toLowerCase(); }
	/ 'plus'i / 'to'i / 'x'i / 'x' / '×' / '+'

symbolSeparator "Ingredient Separator" =
  [&+/]

wordSeparator "Word Separator" =
  sep:'and'i
  	{ return sep.toLowerCase(); }
  / sep:'or'i
  	{ return sep.toLowerCase(); }

quantitySeparator "Quantity Separator" =
  'plus'i / amountSeparator / dashes / symbolSeparator / '+'

ingSeparator "Ingredient Separator" =
	slashes / separator

containerIndicator "Container Indicators" =
  '(' { return '('; }
  / '{' { return '('; }
  / '[' { return '('; }
  / ')' { return ')'; }
  / '}' { return ')'; }
  / ']' { return ')'; }


/*----------  Keywords  ----------*/
fillerKeyword "Ignored" =
	"approximately"i / "combination"i / "additional"i / "optional"i / "without"i / "another"i / "enough"i / "either"i / "before"i / "around"i / "approx"i / "worth"i / "their"i / "other"i / "or-so"i / "or so"i / "minus"i / "equal"i / "about"i / "your"i / "with"i / "then"i / "that"i / "than"i / "such"i / "plus"i / "over"i / "orso"i / "more"i / "like"i / "less"i / "just"i / "into"i / "i.e."i / "high"i / "from"i / "each"i / "e.g."i / "you"i / "use"i / "too"i / "the"i / "see"i / "per"i / "out"i / "not"i / "low"i / "for"i / "few"i / "but"i / "all"i / "up"i / "to"i / "so"i / "on"i / "of"i / "no"i / "my"i / "in"i / "if"i / "ie"i / "eg"i / "ea"i / "at"i / "as"i / "an"i / "i"i / "a"i / "~"

amountKeyword "Amount Keyword" =
	"half a dozen"i / "seventeen"i / "thirteen"i / "quarter-"i / "quarter "i / "numerous"i / "nineteen"i / "fourteen"i / "eighteen"i / "sixteen"i / "several"i / "seventy"i / "quarter"i / "hundred"i / "fifteen"i / "amounts"i / "twenty"i / "twelve"i / "thirty"i / "single"i / "ninety"i / "eleven"i / "eighty"i / "couple"i / "amount"i / "three"i / "third"i / "sixty"i / "sixth"i / "seven"i / "forty"i / "fifty"i / "eight"i / "dozen"i / "ten-"i / "ten "i / "some"i / "nine"i / "half"i / "four"i / "five"i / "two"i / "ten"i / "six"i / "one"i / "lot"i

descriptorKeyword "Descriptor Keyword" =
	"double-strength"i / "double strength"i / "doublestrength"i / "waltnut-sized"i / "waltnut sized"i / "unpasteurized"i / "traditionally"i / "sashimi-grade"i / "sashimi grade"i / "reconstituted"i / "ready-to-cook"i / "quarter-sized"i / "quarter sized"i / "no-salt-added"i / "double-acting"i / "double acting"i / "berry-forward"i / "berry forward"i / "approximately"i / "accompaniment"i / "waltnutsized"i / "sashimigrade"i / "salt-reduced"i / "salt reduced"i / "refrigerated"i / "reduced-salt"i / "reduced salt"i / "ready-to-eat"i / "quartersized"i / "quarter-size"i / "quarter size"i / "preservative"i / "off the bone"i / "nixtamalized"i / "hydrogenated"i / "hand-chopped"i / "hand chopped"i / "doubleacting"i / "double-thick"i / "double thick"i / "concentrated"i / "berryforward"i / "walnut-size"i / "walnut size"i / "unsweetened"i / "unsulphured"i / "unflavoured"i / "tri-colored"i / "tri colored"i / "traditional"i / "tender-stem"i / "tender stem"i / "temperature"i / "sustainably"i / "sustainable"i / "sushi-grade"i / "sushi grade"i / "saltreduced"i / "responsibly"i / "reducedsalt"i / "rectangular"i / "recommended"i / "quartersize"i / "pasteurized"i / "non-iodized"i / "non iodized"i / "middle-size"i / "middle size"i / "liposoluble"i / "king-arthur"i / "king arthur"i / "immediately"i / "handchopped"i / "flavourless"i / "doublethick"i / "disc-shaped"i / "disc shaped"i / "cylindrical"i / "butterflied"i / "burger-size"i / "burger size"i / "accumulated"i / "walnutsize"i / "vegetarian"i / "unsulpured"i / "unsulfured"i / "unseasoned"i / "unprepared"i / "unleavened"i / "unflavored"i / "unfiltered"i / "unbleached"i / "tricolored"i / "thoroughly"i / "tenderstem"i / "sushigrade"i / "spiralized"i / "spiralised"i / "solidified"i / "smokehouse"i / "relatively"i / "rehydrated"i / "pulverized"i / "previously"i / "preferably"i / "on the cob"i / "noniodized"i / "middlesize"i / "low-starch"i / "low starch"i / "loose-leaf"i / "loose leaf"i / "lengthwise"i / "kingarthur"i / "individual"i / "granulated"i / "generously"i / "gelatinous"i / "full-cream"i / "full cream"i / "flavourful"i / "flavorless"i / "equivalent"i / "double-cut"i / "double cut"i / "discshaped"i / "diagonally"i / "dessicated"i / "desiccated"i / "decorative"i / "completely"i / "commercial"i / "burgersize"i / "assortment"i / "acidulated"i / "wholemeal"i / "untrimmed"i / "untreated"i / "unsprayed"i / "unskinned"i / "unroasted"i / "unripened"i / "unrefined"i / "undiluted"i / "thickness"i / "thickened"i / "sweetened"i / "sprinkled"i / "sometimes"i / "separated"i / "segmented"i / "scrambled"i / "scattered"i / "salt-free"i / "salt free"i / "quartered"i / "purchased"i / "processed"i / "preferred"i / "powedered"i / "pin-boned"i / "pin boned"i / "pesticide"i / "perfectly"i / "non-stick"i / "non stick"i / "neutrally"i / "necessary"i / "naturally"i / "miniature"i / "marinated"i / "macerated"i / "lowstarch"i / "looseleaf"i / "julienned"i / "instantly"i / "imitation"i / "hong-kong"i / "hong kong"i / "generally"i / "garnishes"i / "garnished"i / "fullcream"i / "flavoured"i / "flavorful"i / "flattened"i / "flat-leaf"i / "flat leaf"i / "finishing"i / "fermented"i / "favourite"i / "fashioned"i / "extremely"i / "extracted"i / "excellent"i / "elongated"i / "doublecut"i / "distilled"i / "dissolved"i / "discarded"i / "different"i / "diastatic"i / "deshelled"i / "defrosted"i / "decorated"i / "crustless"i / "congealed"i / "condensed"i / "certified"i / "beautiful"i / "available"i / "authentic"i / "artisanal"i / "aluminium"i / "activated"i / "whatever"i / "unsmoked"i / "unsliced"i / "unpitted"i / "unpeeled"i / "unhulled"i / "uncooked"i / "tolerant"i / "titanium"i / "tempered"i / "suitable"i / "strongly"i / "stripped"i / "strength"i / "strained"i / "standard"i / "squeezed"i / "softened"i / "smoothed"i / "smallish"i / "smallest"i / "slivered"i / "slightly"i / "skinless"i / "sizzling"i / "simmered"i / "shredded"i / "seedless"i / "seasoned"i / "seasonal"i / "scrubbed"i / "scrapped"i / "sandwich"i / "saltfree"i / "rindless"i / "ribboned"i / "reserved"i / "required"i / "rendered"i / "purified"i / "prepared"i / "postcard"i / "pinboned"i / "pastured"i / "packaged"i / "original"i / "ordinary"i / "nonstick"i / "moisture"i / "low-salt"i / "low salt"i / "la-style"i / "la style"i / "julienne"i / "inserted"i / "imported"i / "hydrated"i / "hongkong"i / "heritage"i / "headless"i / "grounded"i / "generous"i / "garlicky"i / "full-fat"i / "full fat"i / "frenched"i / "fragrant"i / "flavored"i / "flatleaf"i / "filtered"i / "favorite"i / "fattiest"i / "european"i / "enriched"i / "delicate"i / "cylindar"i / "cultured"i / "culinary"i / "crumpled"i / "crumbled"i / "corn-fed"i / "corn fed"i / "coloured"i / "colorful"i / "coarsley"i / "coarsely"i / "circular"i / "chrunchy"i / "buttered"i / "boneless"i / "bone-out"i / "bone out"i / "bleached"i / "blanched"i / "assorted"i / "arranged"i / "aromatic"i / "adjusted"i / "additive"i / "actually"i / "x-large"i / "x large"i / "whisked"i / "whacked"i / "vintage"i / "variety"i / "unwaxed"i / "uncured"i / "unbaked"i / "trussed"i / "trimmed"i / "topping"i / "toasted"i / "to-cook"i / "to cook"i / "tightly"i / "thickly"i / "tex-mex"i / "tex mex"i / "tasting"i / "streaky"i / "stirred"i / "stemmed"i / "stem-on"i / "stem on"i / "steeped"i / "steamed"i / "spelled"i / "special"i / "sourced"i / "soluble"i / "snipped"i / "smoking"i / "smeared"i / "smashed"i / "smaller"i / "slender"i / "skinned"i / "skimmed"i / "similar"i / "shelled"i / "shallow"i / "scraped"i / "scooped"i / "savoury"i / "sautéed"i / "sauteed"i / "rounded"i / "roughly"i / "roasted"i / "ripened"i / "reserve"i / "removed"i / "regular"i / "refried"i / "refined"i / "reduced"i / "quality"i / "protein"i / "proofed"i / "process"i / "pricked"i / "pressed"i / "premium"i / "powdery"i / "pounded"i / "poached"i / "pickled"i / "perfect"i / "peppery"i / "pasture"i / "organic"i / "no-salt"i / "no salt"i / "nitrate"i / "neutral"i / "natural"i / "mixture"i / "melting"i / "matured"i / "marbled"i / "lowsalt"i / "loosely"i / "lightly"i / "lighter"i / "liberal"i / "leanest"i / "lastyle"i / "largely"i / "kneaded"i / "instant"i / "ideally"i / "highest"i / "heaping"i / "healthy"i / "head-on"i / "head on"i / "grilled"i / "grandma"i / "grained"i / "grade-b"i / "grade-a"i / "grade b"i / "grade a"i / "gourmet"i / "garnish"i / "fullfat"i / "freshly"i / "freezer"i / "fleshed"i / "fattier"i / "exactly"i / "english"i / "dressed"i / "drained"i / "divided"i / "diluted"i / "desired"i / "denuded"i / "crushed"i / "crunchy"i / "crumbly"i / "crisply"i / "crisped"i / "cracked"i / "covered"i / "country"i / "cornfed"i / "cooking"i / "colored"i / "cleaned"i / "classic"i / "citrusy"i / "chunked"i / "chopped"i / "chilled"i / "checked"i / "charred"i / "candied"i / "buttery"i / "butcher"i / "burning"i / "bunched"i / "brushed"i / "bruised"i / "browned"i / "broiled"i / "braised"i / "bottled"i / "boneout"i / "bone-in"i / "bone in"i / "boiling"i / "bloomed"i / "blended"i / "average"i / "artisan"i / "alcohol"i / "zested"i / "xlarge"i / "wilted"i / "whole-"i / "whole "i / "watery"i / "washed"i / "warmed"i / "virgin"i / "veined"i / "vacuum"i / "unused"i / "unripe"i / "undyed"i / "tossed"i / "topped"i / "tocook"i / "to-eat"i / "to eat"i / "tinned"i / "thinly"i / "thawed"i / "texmex"i / "tender"i / "tasted"i / "syrupy"i / "sturdy"i / "strong"i / "stoned"i / "sticky"i / "stewed"i / "stemon"i / "spongy"i / "spiral"i / "soured"i / "softly"i / "sodium"i / "soaked"i / "smooth"i / "smoked"i / "sliced"i / "skinon"i / "skinny"i / "simple"i / "silver"i / "sifted"i / "sieved"i / "shaved"i / "shaped"i / "shaken"i / "served"i / "seeded"i / "season"i / "seared"i / "scored"i / "scaled"i / "sauted"i / "salted"i / "rustic"i / "rubbed"i / "rolled"i / "robust"i / "rising"i / "ripped"i / "ripest"i / "rinsed"i / "ridged"i / "really"i / "raised"i / "puréed"i / "pureéd"i / "pureed"i / "pulsed"i / "pulled"i / "poured"i / "podded"i / "plenty"i / "planed"i / "placed"i / "pitted"i / "picked"i / "petite"i / "person"i / "pencil"i / "peeled"i / "peated"i / "patted"i / "packed"i / "opened"i / "nosalt"i / "normal"i / "needed"i / "native"i / "narrow"i / "minced"i / "milled"i / "mildly"i / "melted"i / "mellow"i / "medium"i / "mature"i / "massel"i / "mashed"i / "living"i / "little"i / "liquid"i / "leaved"i / "leaner"i / "larger"i / "juiced"i / "jarred"i / "husked"i / "hulled"i / "heated"i / "hearty"i / "heaped"i / "headon"i / "halved"i / "ground"i / "gritty"i / "greasy"i / "grated"i / "grainy"i / "gradeb"i / "gradea"i / "gluten"i / "glazed"i / "fruity"i / "frozen"i / "frosty"i / "freeze"i / "formed"i / "forced"i / "folded"i / "fluffy"i / "floury"i / "floral"i / "fleshy"i / "flakey"i / "flaked"i / "firmly"i / "finest"i / "finely"i / "farmed"i / "fairly"i / "exotic"i / "edible"i / "eating"i / "earthy"i / "decent"i / "darker"i / "crusty"i / "crispy"i / "creamy"i / "course"i / "cooled"i / "cooked"i / "colour"i / "coated"i / "coarse"i / "chunky"i / "chubby"i / "choice"i / "cheesy"i / "chalky"i / "centre"i / "center"i / "caught"i / "casing"i / "carved"i / "canned"i / "broken"i / "bright"i / "brewed"i / "bought"i / "bonein"i / "boiled"i / "bodied"i / "bitter"i / "bigger"i / "beaten"i / "basted"i / "barrel"i / "barely"i / "baking"i / "always"i / "almost"i / "adjust"i / "active"i / "action"i / "acting"i / "-style"i / " style"i / "zesty"i / "young"i / "wiped"i / "whole"i / "wafer"i / "vegan"i / "under"i / "uncut"i / "trade"i / "tough"i / "total"i / "toeat"i / "thumb"i / "thick"i / "texas"i / "tepid"i / "tasty"i / "taste"i / "tangy"i / "sweet"i / "super"i / "sunny"i / "style"i / "store"i / "stone"i / "still"i / "stale"i / "split"i / "spicy"i / "solid"i / "smoky"i / "smoke"i / "small"i / "sized"i / "short"i / "shell"i / "sharp"i / "shape"i / "serve"i / "scant"i / "salty"i / "runny"i / "rough"i / "ripen"i / "riced"i / "ready"i / "rapid"i / "range"i / "quick"i / "proof"i / "press"i / "point"i / "plump"i / "plant"i / "plain"i / "pared"i / "paper"i / "outer"i / "oiled"i / "nutty"i / "mushy"i / "multi"i / "moons"i / "moist"i / "mixed"i / "minty"i / "mince"i / "milky"i / "micro"i / "meaty"i / "mealy"i / "lower"i / "loose"i / "local"i / "level"i / "leave"i / "least"i / "leafy"i / "large"i / "knead"i / "jumbo"i / "juicy"i / "jammy"i / "inner"i / "house"i / "hefty"i / "heavy"i / "hardy"i / "half-"i / "half "i / "grind"i / "grass"i / "grain"i / "grade"i / "giant"i / "fully"i / "fried"i / "fresh"i / "flaky"i / "fizzy"i / "fishy"i / "fiery"i / "fatty"i / "fancy"i / "extra"i / "dutch"i / "dried"i / "diced"i / "dairy"i / "curly"i / "cured"i / "cubed"i / "crust"i / "cross"i / "crisp"i / "cored"i / "color"i / "clear"i / "clean"i / "chewy"i / "cheap"i / "caged"i / "bushy"i / "burnt"i / "brush"i / "broad"i / "boxed"i / "boned"i / "blind"i / "bland"i / "basic"i / "based"i / "baked"i / "added"i / "wild"i / "wide"i / "well"i / "weak"i / "waxy"i / "warm"i / "vine"i / "very"i / "used"i / "ugly"i / "type"i / "torn"i / "tiny"i / "tied"i / "thin"i / "temp"i / "stir"i / "sour"i / "soft"i / "slow"i / "slit"i / "skin"i / "skim"i / "size"i / "side"i / "shop"i / "semi"i / "room"i / "rise"i / "ripe"i / "rich"i / "real"i / "rare"i / "pure"i / "pith"i / "part"i / "over"i / "oven"i / "oily"i / "note"i / "nice"i / "navy"i / "much"i / "most"i / "more"i / "moon"i / "mini"i / "mild"i / "meat"i / "many"i / "made"i / "lump"i / "luke"i / "long"i / "live"i / "like"i / "less"i / "left"i / "lean"i / "kind"i / "just"i / "iced"i / "home"i / "high"i / "heat"i / "hard"i / "hand"i / "half"i / "good"i / "full"i / "free"i / "food"i / "flat"i / "firm"i / "fire"i / "fine"i / "fast"i / "fair"i / "easy"i / "dull"i / "dish"i / "dice"i / "deli"i / "deep"i / "dark"i / "cool"i / "cook"i / "cold"i / "char"i / "cave"i / "carb"i / "cage"i / "bulk"i / "boil"i / "bite"i / "best"i / "baby"i / "aged"i / "wet"i / "top"i / "raw"i / "pre"i / "par"i / "pan"i / "old"i / "off"i / "non"i / "new"i / "msc"i / "mix"i / "mid"i / "med"i / "ish"i / "icy"i / "ice"i / "hot"i / "gmo"i / "fed"i / "fat"i / "dry"i / "diy"i / "day"i / "cut"i / "big"i / "any"i / "air"i / "add"i / "xl"i / "us"i / "un"i / "sm"i / "re"i / "md"i / "lg"i / "de"i / "bi"i

unitKeyword "Unit Keyword" =
	"servings-worth"i / "servings worth"i / "servingsworth"i / "sprinklings"i / "containters"i / "wine-glass"i / "wine glass"i / "sprinkling"i / "selections"i / "scattering"i / "quantities"i / "containter"i / "containers"i / "bar-spoons"i / "bar spoons"i / "wineglass"i / "sprinkles"i / "spoonfuls"i / "shoulders"i / "selection"i / "marylands"i / "hand-full"i / "hand full"i / "grindings"i / "envelopes"i / "container"i / "carcasses"i / "barspoons"i / "bar-spoon"i / "bar spoon"i / "trotters"i / "squeezes"i / "sprinkle"i / "spoonful"i / "splashes"i / "shoulder"i / "shavings"i / "servings"i / "segments"i / "sections"i / "quantity"i / "portions"i / "peelings"i / "packages"i / "maryland"i / "handfuls"i / "handfull"i / "grinding"i / "gratings"i / "fistfuls"i / "filamets"i / "envelope"i / "drizzles"i / "crumbles"i / "clusters"i / "branches"i / "barspoon"i / "trotter"i / "throats"i / "threads"i / "teabags"i / "tablets"i / "strands"i / "squeeze"i / "squares"i / "spheres"i / "sleeves"i / "shaving"i / "serving"i / "segment"i / "section"i / "scrapes"i / "sachets"i / "reserve"i / "recipes"i / "rashers"i / "punnets"i / "pouches"i / "portion"i / "pinches"i / "packets"i / "package"i / "lobules"i / "lengths"i / "lardons"i / "kernels"i / "handful"i / "grating"i / "glasses"i / "florets"i / "fistful"i / "fillets"i / "filamet"i / "drizzle"i / "dollops"i / "cutlets"i / "crumble"i / "cluster"i / "circles"i / "carcass"i / "bundles"i / "bunches"i / "breasts"i / "bowlful"i / "bottles"i / "batches"i / "wheels"i / "wedges"i / "twists"i / "thumbs"i / "throat"i / "thread"i / "thighs"i / "teabag"i / "tablet"i / "strips"i / "strand"i / "sticks"i / "stalks"i / "square"i / "sprigs"i / "spoons"i / "splash"i / "sphere"i / "spears"i / "slurry"i / "slices"i / "sleeve"i / "shells"i / "sheets"i / "shards"i / "shanks"i / "shakes"i / "scoops"i / "sachet"i / "rounds"i / "recipe"i / "rashes"i / "rasher"i / "quills"i / "punnet"i / "pieces"i / "petals"i / "packet"i / "lobule"i / "loaves"i / "length"i / "leaves"i / "layers"i / "lardon"i / "ladles"i / "kernel"i / "joints"i / "hearts"i / "halves"i / "grinds"i / "grates"i / "grains"i / "fronds"i / "floret"i / "fillet"i / "dollop"i / "dashes"i / "cutlet"i / "crowns"i / "cranks"i / "cracks"i / "clumps"i / "cloves"i / "chunks"i / "cheeks"i / "carton"i / "bundle"i / "bricks"i / "breast"i / "branch"i / "bottle"i / "blocks"i / "blades"i / "wings"i / "wheel"i / "wedge"i / "units"i / "twist"i / "twigs"i / "turns"i / "tubes"i / "touch"i / "thumb"i / "thigh"i / "tails"i / "strip"i / "stick"i / "stems"i / "stars"i / "stalk"i / "sprig"i / "spoon"i / "spear"i / "slice"i / "slabs"i / "sides"i / "shots"i / "shell"i / "sheet"i / "shard"i / "shank"i / "seeds"i / "scoop"i / "round"i / "roots"i / "rolls"i / "rings"i / "rinds"i / "racks"i / "quill"i / "puree"i / "pouch"i / "pinch"i / "piece"i / "petal"i / "peels"i / "parts"i / "pairs"i / "packs"i / "nests"i / "necks"i / "links"i / "leafs"i / "layer"i / "ladle"i / "knobs"i / "juice"i / "joint"i / "hunks"i / "heart"i / "heads"i / "grind"i / "glugs"i / "glass"i / "frond"i / "flesh"i / "drops"i / "drips"i / "disks"i / "discs"i / "curls"i / "cubes"i / "crown"i / "count"i / "coins"i / "clump"i / "clove"i / "chunk"i / "cheek"i / "cdita"i / "bunch"i / "bulbs"i / "brick"i / "boxes"i / "bowls"i / "block"i / "blade"i / "beads"i / "batch"i / "balls"i / "arils"i / "zest"i / "wing"i / "unit"i / "twig"i / "turn"i / "tuft"i / "tubs"i / "tube"i / "tins"i / "tail"i / "stem"i / "star"i / "slab"i / "side"i / "shot"i / "seed"i / "root"i / "roll"i / "ring"i / "rind"i / "ribs"i / "rack"i / "q.b."i / "pulp"i / "pots"i / "pods"i / "pkts"i / "peel"i / "pcs."i / "pats"i / "part"i / "pair"i / "pack"i / "nubs"i / "nobs"i / "nest"i / "neck"i / "mugs"i / "lots"i / "logs"i / "loaf"i / "link"i / "legs"i / "leaf"i / "knob"i / "jars"i / "hunk"i / "head"i / "glug"i / "foot"i / "feet"i / "ears"i / "drop"i / "drip"i / "disk"i / "disc"i / "dash"i / "dabs"i / "cuts"i / "curl"i / "cube"i / "ctns"i / "coin"i / "cobs"i / "caps"i / "cans"i / "c.c."i / "bulb"i / "bowl"i / "bits"i / "bars"i / "ball"i / "bags"i / "aril"i / "tub"i / "tin"i / "rib"i / "qty"i / "pot"i / "pod"i / "pkt"i / "pkg"i / "pcs"i / "pat"i / "nub"i / "nos"i / "nob"i / "mug"i / "log"i / "leg"i / "jar"i / "ear"i / "dab"i / "ctn"i / "cob"i / "cda"i / "cap"i / "can"i / "box"i / "bit"i / "bar"i / "bag"i / "qb"i / "pk"i / "pc"i / "ea"i / "dl"i / "cc"i
	
ingredientKeyword "Ingredient Keyword" =
	"flammin hot cheetos"i / "bicarbonate of soda"i / "black onion seeds"i / "all purpose flour"i / "liquid-seasoning"i / "liquid seasoning"i / "hen-of-the-woods"i / "black onion seed"i / "aromatic-bitters"i / "aromatic bitters"i / "sunflower-seeds"i / "sunflower seeds"i / "st.-louis–style"i / "st. louis–style"i / "poaching-liquid"i / "poaching liquid"i / "pickling-liquid"i / "pickling liquid"i / "liquidseasoning"i / "liquid-sweetner"i / "liquid sweetner"i / "cream of tartar"i / "corn-on-the-cob"i / "corn on the cob"i / "coriander-seeds"i / "coriander seeds"i / "braising-liquid"i / "braising liquid"i / "aromaticbitters"i / "whipping-cream"i / "whipping cream"i / "sweet and sour"i / "sunflowerseeds"i / "sunflower-seed"i / "sunflower seed"i / "st.louis–style"i / "smoked-paprika"i / "smoked paprika"i / "seasoning-salt"i / "seasoning-cube"i / "seasoning salt"i / "seasoning cube"i / "powdered-sugar"i / "powdered sugar"i / "poachingliquid"i / "picklingliquid"i / "pickling-juice"i / "pickling juice"i / "new york strip"i / "liquidsweetner"i / "fermented-bean"i / "fermented bean"i / "corianderseeds"i / "coriander-seed"i / "coriander seed"i / "canning-liquid"i / "canning liquid"i / "braisingliquid"i / "baby back ribs"i / "whippingcream"i / "whipped-cream"i / "whipped cream"i / "sweet-peppers"i / "sweet peppers"i / "sunflowerseed"i / "smokedpaprika"i / "sheet-gelatin"i / "sheet gelatin"i / "seasoningsalt"i / "seasoningcube"i / "refried-beans"i / "refried beans"i / "ras-el-hanout"i / "powderedsugar"i / "picklingjuice"i / "mediterranean"i / "leaf-gelatine"i / "leaf gelatine"i / "hot-chocolate"i / "hot chocolate"i / "half-and-half"i / "half and half"i / "glass-noodles"i / "glass noodles"i / "ginger-garlic"i / "ginger garlic"i / "food-coloring"i / "food coloring"i / "fermentedbean"i / "crown-daisies"i / "crown daisies"i / "corianderseed"i / "cooking-spray"i / "cooking spray"i / "canningliquid"i / "bitter-greens"i / "bitter greens"i / "belly of pork"i / "banana-leaves"i / "banana leaves"i / "baking-powder"i / "baking powder"i / "baby back rib"i / "angelica-root"i / "angelica root"i / "whippedcream"i / "sweetpeppers"i / "sweet-potato"i / "sweet potato"i / "simple-syrup"i / "simple syrup"i / "sheetgelatin"i / "sesame-seeds"i / "sesame seeds"i / "refriedbeans"i / "medjool-date"i / "medjool date"i / "liquid-smoke"i / "liquid smoke"i / "leafgelatine"i / "hotchocolate"i / "hot dog buns"i / "glassnoodles"i / "glass-noodle"i / "glass noodle"i / "gingergarlic"i / "foodcoloring"i / "eye of round"i / "dried-shrimp"i / "dried shrimp"i / "dinner-rolls"i / "dinner rolls"i / "curry-leaves"i / "curry leaves"i / "crowndaisies"i / "country-ribs"i / "country ribs"i / "cookingspray"i / "celery-seeds"i / "celery seeds"i / "bittergreens"i / "bananaleaves"i / "bakingpowder"i / "angelicaroot"i / "whole-wheat"i / "whole wheat"i / "wheat-grass"i / "wheat grass"i / "vine-leaves"i / "vine leaves"i / "taco-shells"i / "taco shells"i / "sweety-drop"i / "sweety drop"i / "sweetpotato"i / "sweet-sauce"i / "sweet sauce"i / "st.-germain"i / "st. germain"i / "simplesyrup"i / "short-crust"i / "short crust"i / "sesameseeds"i / "self-rising"i / "self rising"i / "root-ginger"i / "root ginger"i / "poppy-seeds"i / "poppy seeds"i / "multi-grain"i / "multi grain"i / "medjooldate"i / "liquidsmoke"i / "hot-peppers"i / "hot-paprika"i / "hot-italian"i / "hot-chilies"i / "hot peppers"i / "hot paprika"i / "hot italian"i / "hot dog bun"i / "hot chilies"i / "home-cooked"i / "home cooked"i / "half & half"i / "glassnoodle"i / "driedshrimp"i / "dinnerrolls"i / "curryleaves"i / "countryribs"i / "country-rib"i / "country rib"i / "celeryseeds"i / "celery-seed"i / "celery seed"i / "bean-thread"i / "bean thread"i / "banana-stem"i / "banana stem"i / "baking-soda"i / "baking soda"i / "wholewheat"i / "wheatgrass"i / "vineleaves"i / "tart-shell"i / "tart shell"i / "tacoshells"i / "taco-shell"i / "taco shell"i / "sweetydrop"i / "sweetsauce"i / "st.germain"i / "st-germain"i / "st germain"i / "split-gram"i / "split gram"i / "spare-ribs"i / "spare ribs"i / "sour-cream"i / "sour cream"i / "soft-shell"i / "soft shell"i / "shortening"i / "shortcrust"i / "short-ribs"i / "short ribs"i / "selfrising"i / "seasonings"i / "rootginger"i / "poppyseeds"i / "poppy-seed"i / "poppy seed"i / "navy-beans"i / "navy beans"i / "multigrain"i / "long-beans"i / "long beans"i / "hotpeppers"i / "hotpaprika"i / "hotitalian"i / "hotchilies"i / "hot-pepper"i / "hot pepper"i / "homecooked"i / "hero-rolls"i / "hero rolls"i / "gram-flour"i / "gram flour"i / "flatbreads"i / "five-spice"i / "five spice"i / "eye-fillet"i / "eye fillet"i / "curry-leaf"i / "curry leaf"i / "countryrib"i / "celeryseed"i / "burnt-ends"i / "burnt ends"i / "beanthread"i / "bay-leaves"i / "bay leaves"i / "bananastem"i / "bakingsoda"i / "animal-fat"i / "animal fat"i / "'00'-flour"i / "'00' flour"i / "tartshell"i / "tacoshell"i / "sweetener"i / "sun-dried"i / "sun dried"i / "sub-rolls"i / "sub rolls"i / "stgermain"i / "steel-cut"i / "steel cut"i / "sprinkles"i / "splitgram"i / "spareribs"i / "spare-rib"i / "spare rib"i / "sourdough"i / "sourcream"i / "softshell"i / "shortribs"i / "short-rib"i / "short rib"i / "seasoning"i / "rib-roast"i / "rib roast"i / "prime-rib"i / "prime rib"i / "pot-roast"i / "pot roast"i / "poppyseed"i / "pie-shell"i / "pie shell"i / "navybeans"i / "navy-bean"i / "navy bean"i / "longbeans"i / "hotpepper"i / "hot-sauce"i / "hot-house"i / "hot-chili"i / "hot-chile"i / "hot sauce"i / "hot house"i / "hot chili"i / "hot chile"i / "herorolls"i / "hero-roll"i / "hero roll"i / "gramflour"i / "gold-leaf"i / "gold leaf"i / "flatbread"i / "flat-iron"i / "flat iron"i / "fivespice"i / "eyefillet"i / "deli-meat"i / "deli meat"i / "deep-dish"i / "deep dish"i / "curryleaf"i / "cold-brew"i / "cold brew"i / "burntends"i / "bayleaves"i / "barbecued"i / "back-ribs"i / "back ribs"i / "baby-back"i / "baby back"i / "animalfat"i / "'00'flour"i / "sweetner"i / "sundried"i / "subrolls"i / "sub-roll"i / "sub roll"i / "steelcut"i / "sparerib"i / "shortrib"i / "ribroast"i / "rib-tips"i / "rib tips"i / "primerib"i / "potroast"i / "potatoes"i / "pieshell"i / "navybean"i / "hotsauce"i / "hothouse"i / "hotchili"i / "hotchile"i / "hot-dogs"i / "hot dogs"i / "heroroll"i / "goldleaf"i / "flatiron"i / "flathead"i / "delimeat"i / "deepdish"i / "coldbrew"i / "bay-leaf"i / "bay leaf"i / "barbecue"i / "backribs"i / "back-rib"i / "back-fat"i / "back rib"i / "back fat"i / "babyback"i / "00-flour"i / "00 flour"i / "tri-tip"i / "tri tip"i / "tartare"i / "subroll"i / "ribtips"i / "rib-eye"i / "rib eye"i / "old-bay"i / "old bay"i / "medjool"i / "madeira"i / "hotdogs"i / "hot-dog"i / "hot dog"i / "fatback"i / "extract"i / "dry-rub"i / "dry rub"i / "candied"i / "bayleaf"i / "backrib"i / "backfat"i / "5-spice"i / "5 spice"i / "00flour"i / "tritip"i / "tartar"i / "ribeye"i / "potato"i / "oldbay"i / "lumpia"i / "longan"i / "hotdog"i / "ener-g"i / "ener g"i / "dryrub"i / "cloves"i / "5spice"i / "rolls"i / "pig's"i / "liver"i / "energ"i / "clove"i / "v.s."i / "biga"i / "s&b"i / "vs"i

/*----------  Fillers  ----------*/
// should be allowed to start and end with or without spaces
// includes keywords, punctuation, and containers
fillerExpression "Filler Expression" =
		// "~ " / "~ approx " / "~ approx ~"
  symbols0:filler+ ws:_* words:$(fillerKeyword _+)* symbols1:filler*
  	{ return formatFillerExpression(symbols0, ws, words, symbols1); }
  // " approx "
  / symbols0:filler* ws:_* words:$(fillerKeyword _+)+ symbols1:filler*
  	{ return formatFillerExpression(symbols0, ws, words, symbols1); }
  // "~" / "approx " / " !" 
  / symbols0:filler* ws:_* words:$(fillerKeyword _*)* symbols1:filler+
  	{ return formatFillerExpression(symbols0, ws, words, symbols1); }

filler "Filler" =
	container
	/ fillerPunctuation

fillerPunctuation "Ingored Punctuation" =
	[~*.!?©™]

/*----------  Containers  ----------*/
// these could be expanded to parse out their innards, but i'm just not feeling it
containers "Containers" =
	head:container tail:$(_* container)*
	{
		return head.concat(tail);
	}

container "Container" =
	opening:containerIndicator txt:$(containerSymbol)* closing:containerIndicator
	{
		return opening + txt.trim().toLowerCase() + closing;
	}

containerSymbol "Container Symbol" = 
	!containerIndicator char:. { return char; }

/*=====  End of Base Rules  ======*/


/*=======================================
=            Ingredient Lines            =
=======================================*/
ingredientLines "Ingredient Lines" =
	ingredientLine _+ wordSeparator _+ wordSeparator
	/ ingredientLine

ingredientLine "Ingredient Line" =
	$('+' _*)?
	qty:quantityExpressions?
	ing:ingredientExpression
	com:commentExpression?
		{
			return {// WEHRMAN, TODO, you need to safely concat these amount values together
				amounts: (ing.filler[2] && ing.filler[2].amounts && ing.filler[2].amounts.value) ? ing.filler[2].amounts.values : qty.amounts, // { values: [1, 2/3], separator: 'and' }
				unitDescriptor: qty.unitDescriptor, // "large"
				units: qty.units, // { values: [in, cup], separator: 'and' }
				amountUnitSeparator: qty.separator || null, //"plus"
				ingDescriptor: ing.ingDescriptor, // "freshly ground"
				ingredients: ing.ingredients, // { values: [ 'black pepper', 'salt' ], separator: 'and', descriptors: 'washed' }
				comments: com, // ", for serving"
				// filler text is any ignored punctuation or keywords
				// found in and around the interesting bits
				// these include any containerized expressions
				filler: [
					qty.filler[0], // pre-amount
					qty.filler[1], // pre-unitDescriptor
					qty.filler[2], // pre-unit
					ing.filler[0], // pre-ingDescriptor
					ing.filler[1], // pre-ing

					// TODO finish putting these in the correct place 
					// optional secondary qty expression
					ing.filler[2], // TRY qty { amounts: [object], unitDescriptor: null, units: null, fillter: [Array]}
				]
			};
		}

/*=====  End of Ingredient Line  ======*/



/*============================================
=            Quantity Expressions            =
============================================*/
quantityExpressions "Quantity Expressions" =
	head:quantityExpression _+ sep:quantitySeparator _+ tail:quantityExpression
		{
			var headAmount = (head.amounts) ? head.amounts.values : [];
			var tailAmount = (tail.amounts) ? tail.amounts.values : [];

			var headUnit = (head.units) ? head.units.values : [];
			var tailUnit = (tail.units) ? tail.units.values : [];

			var amounts = (head.amounts || tail.amounts) ? { values: [ ...headAmount, ...tailAmount ] } : null;
			var unitDescriptor = (head.unitDescriptor || tail.unitDescriptor) ? { values: [ (head.unitDescriptor) ? head.unitDescriptor.values : null, (tail.unitDescriptor) ? tail.unitDescriptor.values : null ] } : null;
			var units = (head.units || tail.units) ? { values: [ ...headUnit, ...tailUnit ] } : null;

			var headFiller = [ head.filler0 || null, head.filler1 || null, head.filler2 || null ];
			var tailFiller = [ tail.filler0 || null, tail.filler1 || null, tail.filler2 || null ];
			
			return {
				amounts: amounts, // { values: [1, 2/3], separator: 'and' }
				unitDescriptor: unitDescriptor, // "large"
				units: units, // { values: [in, cup], separator: 'and' }
				filler: [ headFiller, tailFiller ],
				separator: sep
			};
		}
		/ quantityExpression

quantityExpression "Quantity Expression" =
  filler0:fillerExpression*
  _*
  amounts:amounts?
  _*
  filler1:fillerExpression*
  _*
  unitDesc:descriptors?
  _*
  filler2:fillerExpression*
  _*
  units:unitExpression?
  	{
  		return {
  			amounts: amounts, // { values: [1, 2/3], separator: 'and' }
	  		unitDescriptor: unitDesc, // "large"
	  		units: units, // { values: [in, cup], separator: 'and' }
	  		filler: [ filler0, filler1, filler2 ]
  		};
  	}


/*----------  Amounts  ----------*/
amounts "Amounts" =
	// 1 2-3
	first:amount _+ second:amount _* sep:dashes+ _* third:amount
		{ return { values: [ first, second, third ], separator: sep }; }
	// 1 to 1 1/4
  	/ first:amount _* dashes* _* sep0:amountSeparator _* dashes* _*  second:amount _* sep1:dashes? _* third:amount
		{ return { values: [ first, second, third ], separator: [ sep0, sep1 ].filter(s => s) }; }
	// 1 3 1/4
  	/ first:amount _* dashes* _*  second:amount _* sep1:dashes? _* third:amount
		{ return { values: [ first, second, third ] }; }
  // 1 - to - 2
	/ first:amount _* dashes* _* sep:amountSeparator _* dashes* _* second:amount
		{ return { values: [ first, second ], separator: sep.toLowerCase() }; }
	// 1x pack salmon
	/ first:amount _* dashes* _* sep:amountSeparator _+ dashes*
		{ return { values: [ first ], separator: sep.toLowerCase() }; }
	// 1 - 2
	/ first:amount _* sep:dashes* _* second:amount
		{ return { values: [ first, second ], separator: sep }; }
	// one
	/ first:amount
		{ return { values: [ first ]}; }

amount "Amount" =
	word:$(amountKeyword _+) { return word.toLowerCase(); }
	/ unicodeAmount
	/ fraction
	/ float
	/ $(digit)+


/*----------  Descriptors  ----------*/
descriptors "Descriptors" =
	// cold and fresh
	head:descriptor _+ sep:wordSeparator _+ tail:descriptor
		{
			return {
				values: [ head, tail ].map(d => d.toLowerCase()),
				separator: sep
			}
		}
	// cold & fresh
	/ head:descriptor _* sep:symbolSeparator _* tail:descriptor
		{
			return {
					values: [ head, tail ].map(d => d.toLowerCase()),
					separator: sep
			}
		}
	// freshly-ground
	/ !ingredientKeyword head:descriptor _* sep:dashes _* tail:descriptor
		{
			return {
				values: [ head, tail ].map(d => d.toLowerCase()),
				separator: sep
			}
		}
      // roughly chopped
  / !ingredientKeyword head:descriptor tail:(','? _+ fillerExpression? _* descriptor)*
		{
			return {
				values: [ head ].concat(tail.map(t => t[4])),
                  filler: tail.map(t => t[2])
			}
		}
	// fresh
	/ !ingredientKeyword desc:descriptor
		{ return { values: desc.toLowerCase() }; }

descriptor "Descriptor" =
	$(descriptorKeyword _* dashes+ _* descriptorKeyword)
  / descriptorKeyword

  // TODO should this also include filler?


/*----------  Units  ----------*/
unitExpression "Unit Expression" =
  // units require an ending space to differentiate them from ingredients
  unit:units { return unit; }

units "Units" =
	// inch knob
	first:unit _+ second:$(dashes? _* unitKeyword _+) // don't be too generic here otherwise it will screw with the ingredient match
		{
			return { values: [ first, second ].map(u => u.toLowerCase().trim()) };
		}
	/ unit:unit _+
		{
			return { values: [ unit.toLowerCase().trim() ] };
		}

unit "Unit" =
	//always needs a following space
	dash:dashes? _* unit:unitKeyword { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:fluidOunce { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:gallon { return (dash) ? dash + unit : unit; }
  // careful on ordering these
  // 'lb' needs to come before 'l'
  // 'gm' needs to come before 'm'
  // 'ml' needs to come before 'm'
  / dash:dashes? _* unit:pound { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:gram { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:liter { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:meter { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:cup { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:inch { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:ounce { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:pint { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:quart { return (dash) ? dash + unit : unit; }
  / dash:dashes? _* unit:spoons { return (dash) ? dash + unit : unit; }


/*----------  Volumes  ----------*/
volume "Volume" =
  fluidOunce
  / cup
  / pint
  / quart
  / gallon
  / liter
  / spoons

fluidOunce "Fluid Ounce" =
  $('fluid'i _* dashes* _* ounce) { return 'fl oz'; }
  / $('fl'i '.'? _* dashes* _* ounce'.'?) { return 'fl oz'; }

cup "Cup" =
  $('cup'i 's'i?) { return 'c'; }
  / $('c'i '.'?) { return 'c'; }

pint "Pint" =
  $('pint'i 's'i?) { return 'pt'; }
  / $('pt'i '.'?) { return 'pt'; }

quart "Quart" =
  $('quart'i 's'i?) { return 'qt'; }
  / $('qt'i 's'i? '.'?) { return 'qt'; }

gallon "Gallon" =
  $('gallon'i 's'i?) { return 'gal'; }
  / $('gal'i 's'i? '.'?) { return 'gal'; }

liter "Liter" =
  $('milliliter'i 's'i?) { return 'ml'; }
  / $('millilitre'i 's'i?) { return 'ml'; }
  / $('centiliter'i 's'i?) { return 'ml'; }
  / $('centilitre'i 's'i?) { return 'ml'; }
  / $('liter'i 's'i?) { return 'l'; }
  / $('litre'i 's'i?) { return 'l'; }
  / $('ltr'i 's'i?) { return 'l'; }
  / $('cl'i 's'i? '.'?) { return 'cl'; }
  / $('ml'i 's'i? '.'?) { return 'ml'; }
  / $('lt'i 's'i? '.'?) { return 'l'; }
  / $('l'i 's'i? '.'?) { return 'l'; }

spoons "Tablespoon/Teaspoon" =
  $('tablespoon'i 's'i? 'ful'i?) { return 'tbsp'; }
  / $('teaspoon'i 's'i?'ful'i?) { return 'tsp'; }
  / $('tblsp'i 's'i? '.'?) { return 'tbsp'; }
  / $('tbsp'i 's'i? '.'?) { return 'tbsp'; }
  / $('tpsp'i 's'i? '.'?) { return 'tbsp'; }
  / $('tsbp'i 's'i? '.'?) { return 'tbsp'; }
  / $('tlb'i 's'i? '.'?) { return 'tbsp'; }
  / $('tbl'i 's'i? '.'?) { return 'tbsp'; }
  / $('tsp'i 's'i? '.'?) { return 'tsp'; }
  / $('tb'i 's'i? '.'?) { return 'tbsp'; }
  / $('T' 's'i? '.'?) { return 'tbsp'; }
  / $('t' 's'i? '.'?) { return 'tsp'; }

/*----------  Weights  ----------*/
weight "Weight" =
  ounce
  / pound
  / gram

ounce "Ounce" =
  $('wt'i '.'? _+ 'ounce'i 's'i?) { return 'oz'; }
  / $('wt'i '.'? _+ 'oz'i 's'i? '.'?) { return 'oz'; }
  / $('ounce'i 's'i?) { return 'oz'; }
  / $('oz'i 's'i? '.'?) { return 'oz'; }

pound "Pound" =
  $('pounds'i) { return 'lbs'; }
  / $('pound'i) { return 'lb'; }
  / $('lbs'i '.'?) { return 'lbs'; }
  / $('lb'i '.'?) { return 'lb'; }

gram "Gram" =
  $('milligram'i 's'i?) { return 'mg'; }
  / $('kilogram'i 's'i?) { return 'kg'; }
  / $('kilo'i 's'i?) { return 'kg'; }
  / $('gram'i 's'i?) { return 'g'; }
  / $('mg'i 's'i? '.'?) { return 'mg'; }
  / $('kg'i 's'i? '.'?) { return 'kg'; }
  / $('gm'i 's'i? '.'?) { return 'g'; }
  / $('gr'i 's'i? '.'?) { return 'g'; }
  / $('g'i 's'i? '.'?) { return 'g'; }
  / $('㎏' 's'i? '.'?) { return 'kg'; }

/*----------  Lengths  ----------*/
length "Length" =
  meter
  / inch
  / foot

foot "Foot" =
	$('feet'i) { return 'ft'; }
	/ $('foot'i) { return 'ft'; }
	/ $('ft'i) { return 'ft'; }
  / $(['`´‘’]) { return 'ft'; }

meter "Meter" =
  $('millimeter'i 's'i?) { return 'mm'; }
  / $('centimeter'i 's'i?) { return 'cm'; }
  / $('meter'i 's'i?) { return 'm'; }
  / $('mm'i 's'i? '.'?) { return 'mm'; }
  / $('cm'i 's'i? '.'?) { return 'cm'; }
  / $('m'i 's'i? '.'?) { return 'm'; }

inch "Inch" =
  $('inch'i 'es'i?) { return 'in'; }
  / $('in'i 's'i? '.'?) { return 'in'; }
  / $(["“”]) { return 'in'; }

/*=====  End of Quantity Expressions  ======*/



/*==============================================
=            Ingredient Expressions            =
==============================================*/
ingredientExpression "Ingredient Expression" =
	//TRY
	$('+' _*)?
	qty:quantityExpressions?	// this really only kicks in when you have head-heavy descriptors (Finely grated zest of 3 lemons")
	//TRY
	filler0:fillerExpression*
  _*
  ingDesc:descriptors?
  _*
  filler1:fillerExpression*
  _*
  ingredients:ingredients
  {
  	return {
  		ingDescriptor: ingDesc, // "freshly ground"
  		ingredients: ingredients, // { values: [ 'black pepper', 'salt' ], separator: 'and', descriptors: 'washed' }
  		filler: [ filler0, filler1, qty ]
  	};
  }

ingredientListEnding "Ingredient List Ending" = 
  ',' _* sep:separator? _* ing:ingredient
  	{
  		return {
  			separator: sep,
  			ingredient: ing
  		};
  	}

ingredients "Ingredients" =
	// banana, apple, pear(, and? pear)
	/*head:ingredient tail:ingredientListEnding*
		{
    	var tailIngredient = tail.map(i => i.ingredient);
    	var tailSeparator = tail.pop().separator;
            
			return {
				values: [ head ].concat(tailIngredient).map(i => i.toLowerCase()),
				separator: (tailSeparator) ? ', ' + tailSeparator : ','
			};
		}
	// apple and fresh peach
	/ 
    */
    head:multiWordIngredient _+ sep:wordSeparator _+ filler:fillerExpression? _* desc:descriptors? _* tail:multiWordIngredient
			{
				return {
					values: [ head, tail ].map(i => i.toLowerCase()),
	        descriptors: desc,
					separator: sep,
	        filler: filler
				};
			}
	
    / head:multiWordIngredient _* sep:ingSeparator _* filler:fillerExpression? _* desc:descriptors? _* tail:multiWordIngredient
			{
				return {
					values: [ head, tail ].map(i => i.toLowerCase()),
					descriptors: desc,
					separator: sep,
	        filler: filler
				};
			}
    / ing:multiWordIngredient
	    {
				return {
					values: [ ing.toLowerCase() ],
				};
			}


// this parses 'blackberries – thawed'
multiWordIngredient "Multi Word Ingredient" =
	//lemon-lime
	head:ingredient tail:$(_+ ingredient)+
    {
			return [ head + tail ].map(i => i.toLowerCase())[0];
		}
    // lemon lime
	/ head:ingredient _+ tail:ingredient
    {
			return head.toLowerCase() + ' ' + tail.toLowerCase();
		}    // lemon
	/ ing:ingredient
		{
			return ing.toLowerCase();
		}

ingredient "Ingredient" =
	// parmigiano-reggiano
	head:ingredientWord !_ dashes+ !_ tail:ingredientWord
		{
			return [ head + '-' + tail ].map(i => i.toLowerCase().trim())[0];
		}
	// apple
	/ ingredientWord

excluded "Excluded" = 
  ingSeparator / fillerKeyword / descriptors / $(amount _* unit) / amount / unit / dashes

// unit's are allowed to start with a dash and that messes with the ingredientWord
excludedWords "Excluded Words" =
  fillerKeyword / descriptors / $(amount _* unit) / amount / wordSeparator

ingredientWord "Ingredient Word" =
	head:ingredientKeyword tail:$(letter)+
		{ return head + tail }
	/ !excludedWords ing:$(letter)+
		{ return ing; }
	/ ing:$(excludedWords $(letter)+)
		{ return ing; }
		// TODO add in quotes that return a consistent formatting !excluded ing:$(letter)+ quotes !excluded ing:$(letter)+


/*=====  End of Ingredient Expressions  ======*/

/*===========================================
=            Comment Expressions            =
===========================================*/
commentExpression "Comment Expression" =
  comment:$(comment*)
  	{ return comment; }


comment "Comment" =
	// match any characters
	char:. { return char; }

/*=====  End of Comment Expressions  ======*/